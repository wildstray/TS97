;****************************************************************************
;**************************** macro list ************************************
;****************************************************************************
;* blank                where           ; riempe la linea di spazi
;* print                where,msg,msg2  ; visualizza il messaggio msg
;* .menu                maxmenu         ; make a selection menu (of maxmenu)
;* .display             num,text1,text2 ; display the menu number num
;* .action    todo }    num             ; if confirmed execute 
;* .back                                ; backward instead forward key
;* .endmenu                             ; current menu termination
;* .input               begin,end,backch; user input beetwen begin and end
;* .maxval              pos,val,prec    ;
;* .minval              pos,val,prec    ;
;* .break               pos,char        ; put a break char in pos
;* .endinput                            ; end of user input
;****************************************************************************

;************************
.macro  blank  where
   ldw     rr6,#where
   ld      r3,#16
   ld      r8,#32
   loop [r3]
   {
      ld      (rr6)+,r8
   }
.endm
;************************
.macro  print  where,msg1,msg2
   pushuw  rr0
   pushuw  rr2
   pushu   r4
   ldw     rr0,#where
   ldw     rr2,msg1
   ld      r4,#16
   loop [r4] {
      lddp (rr0)+,(rr2)+
   }
   .ifc nb msg2
   ldw     rr2,msg2
   ld      r4,#16
   loop [r4] {
      lddp (rr0)+,(rr2)+
   }
   .endc
   popu    r4
   popuw   rr2
   popuw   rr0
.endm
;************************
.macro .menu maxmenu
   ld      r2,#0
   loop {
      ld      r1,DIGIT
      clr     DIGIT
      .ifc nb maxmenu
      if [r1 == #FORWBACK]
      {
         inc      r2
         if [r2 > maxmenu]
         {
            xor    r2,r2
         }
      }
      .endc
.endm
;************************
.macro .next
      inc      r2
.endm
;************************
.macro .display num,text1,text2,calls
   if [r2 == num]
   {
      print line1,#text1
      .ifc nb text2
      print line2,#text2
      .endc
      .ifc nb calls
      call  calls
      .endc
   }
.endm
;************************
.macro .action num
      if [[r2 == num] && [r1 == #CONFIRM]]
      {
.endm
;************************
.macro .back
      if [r1 == #FORWBACK]
      {
         break
      }
.endm
;************************
.macro .endmenu
      ifnobit SWITCH, #PROG
         break
      }
      clr     r1
   }
.endm
;************************
.macro .input minpos,maxpos,backch
   ld      r13,#backch
   ld      r15,#maxpos+1
   ld      r14,#minpos+1
   loop
   {
      ld      r1,DIGIT
      clr     DIGIT
      if [[r8 >= minpos] && [r8 <= r15]]
      {
.endm
;************************
.macro .maxval pos,val,prec
         .ifc b prec
         if [[r1 > #val] && [r1 < #10] && [r8 == #pos]]
         {
            clr     r1
         }
         .endc
         .ifc nb prec
         ld      r9,-(rr10)
         if [[r9 < #10] && [r9 >= #prec] && [r1 > #val] && [r1 < #10] && [r8 == #pos]]
         {
           clr     r1
         }
         incw    rr10
         .endc
.endm
;************************
.macro .minval pos,val,prec
         .ifc b prec
         if [[r1 == #10] && [r8 == #pos]]
         {
            clr     r1
         }
         if [[r1 < #val] && [r1 < #10] && [r8 == #pos]]
         {
            clr     r1
         }
         .endc
         .ifc nb prec
         ld      r9,-(rr10)
         dtmf2bcd r9,r9
         if [[r9 <= #prec] && [r1 == #10] && [r8 == #pos]]
         {
            clr     r1
         }
         if [[r9 <= #prec] && [r1 < #val] && [r1 < #10] && [r8 == #pos]]
         {
           clr     r1
         }
         incw    rr10
         .endc
.endm
;************************
.macro .break pos,char
         if [[r1 != #0] && [r1 <= #10] && [r8 == #pos-1]]
         {
            bcd2ascii  r1,r9
            ld      (rr6)+,r9
            ld      (rr10)+,r1
            ld      r9,#char
            ld      (rr6)+,r9
            ld      r9,#95
            ld      (rr6),r9
            inc     r8
            inc     r8
            clr     r1
         }
         if [[r1 == #FORWBACK] && [r8 == #pos+1]]
         {
            if [r13 == #32]
            {
               ld      r1,#0
               ld      -(rr10),r1
               ld      r9,r13
            } else {
               ld      r13,(rr10)
               bcd2ascii r13,r9
               decw    rr10 
            }
            ld      (rr6),r9
            ld      r9,#char 
            ld      -(rr6),r9
            dec     r8
            ld      r9,#95
            ld      -(rr6),r9
            dec     r8
            clr     r1
         }
.endm
;************************
.macro .endinput
         if [[r1 != #0] && [r1 <= #10] && [r8 < r15]]
         {
            bcd2ascii  r1,r9
            ld      (rr6)+,r9
            ld      (rr10)+,r1
            ld      r9,#95
            ld      (rr6),r9
            inc     r8
            clr     r1
         }
         if [[r1 == #FORWBACK] && [r8 == r15]]
         {
            ld      r9,#32
            ld      (rr6),r9
            decw    rr10
            ld      r9,#95
            ld      -(rr6),r9
            dec     r8
            clr     r1
         }
         if [[r1 == #FORWBACK] && [r8 >= r14]] 
         {
            if [r13 == #32]
            {
               ld      r1,#0
               ld      -(rr10),r1
               ld      r9,r13
            } else {
               ld      r13,(rr10)
               bcd2ascii r13,r9
               decw    rr10 
            }
            ld      (rr6),r9
            ld      r9,#95
            ld      -(rr6),r9
            dec     r8
            clr     r1
         }
         if [[r1 == #CONFIRM] && [r8 == r15]]
         {
            ld      r9,#32
            ld      (rr6),r9
            break
         }
         if [r1 == #CONFIRM]
         {
            ld      r13,(rr10)
            if [r13 == #0]
            {
               ld      r9,#32
            } else {
               bcd2ascii r13,r9
            }
            ld      (rr6),r9
            break
         }
      }
      clr     r1
   }
.endm
;************************

























